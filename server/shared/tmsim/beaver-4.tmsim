// 4-state busy beaver Turing Machine example

// See how many 1's we can write on a blank tape using
// only a four-state Turing Machine and one non-blank symbol
states A B C D  // list of state names, first is starting state
symbols 1       // list of symbols (- is blank cell)
tape test -     // initial tape contents, blank in this case

// This TM writes 13 1s in 107 steps.

// specify transistions: action state symbol state' write move
//    state = the current state of the FSM
//    symbol = the symbol read from the current cell
//    state' = state on the next cycle 
//    write = symbol to be written into the current cell
//    move = tape movement ("l" = left, "r" = right, "-"=stay put)
//    old  R   new  W M
action A   -    B   1 r
action A   1    B   1 l
action B   -    A   1 l
action B   1    C   - l
action C   - *halt* 1 r
action C   1    D   1 l
action D   -    D   1 r
action D   1    A   - r
