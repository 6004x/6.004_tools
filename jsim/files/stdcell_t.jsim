// 6.004 standard cell library (3/5/00)
// Modified for New JSim in August 2013

/********************************************************************************
*********************************************************************************
***
***  memory options
***
*********************************************************************************
********************************************************************************/

.options mem_cin=.005pf	// input terminal capacitance (F)
.options mem_cout=0	// output terminal capacitance (F)
.options mem_tr=1000	// output rise time (s/F)
.options mem_tf=500	// output fall time (s/F)
.options mem_tpd_regfile=2ns	// tpd when nlocs <= 128 (s)
.options mem_tpd_sram=4ns	// tpd when 128 < nlocs <= 1024 (s)
.options mem_tpd_dram=40ns	// tpd when nlocs > 1024 (s)
.options mem_tcd=20ps	// contamination delay (s)

.options mem_size_sram=5	// size of static memory cell (u**2)
.options mem_size_access=1	// size of access fet to mem cell (u**2)
.options mem_size_address_buffer=20 // size of memory address buffer (u**2)
.options mem_size_address_decoder=4 // size of 1 bit's worth of memory address decoder (u**2)
.options mem_size_output_buffer=30  // size of memory output buffer (u**2)
.options mem_size_write_buffer=20  // size of memory write data buffer (u**2)

/********************************************************************************
*********************************************************************************
***
***  constants, buffers and inverters
***
*********************************************************************************
********************************************************************************/

.subckt constant0 z
.connect z gnd
.ends

.subckt constant1 z
.connect z vdd
.ends

.subckt buffer a z 
Xinv1 inverter a n1
Xinv2 inverter n1 z
.ends

.subckt buffer_2 a z 
Xinv1 inverter a n1
Xinv2 inverter_2 n1 z
.ends

.subckt buffer_4 a z 
Xinv1 inverter a n1
Xinv2 inverter_4 n1 z
.ends

.subckt buffer_8 a z 
Xinv1 inverter_2 a n1
Xinv2 inverter_8 n1 z
.ends

.subckt inverter a z
N1 z a gnd w=4
P1 vdd a z w=16
.ends

.subckt inverter_2 a z
N1 z a gnd w=8
P1 vdd a z w=32
.ends

.subckt inverter_4 a z
N1 z a gnd w=16
P1 vdd a z w=64
.ends

.subckt inverter_8 a z
N1 z a gnd w=32
P1 vdd a z w=128
.ends

.subckt tristate e a z
Xeinv inverter e en
Xnand nand2 e a n1
Xnor nor2 en a n2
N2 z n2 gnd w=4
N1 vdd n1 z w=16
.ends

.subckt tristate_2 e a z
Xeinv inverter e en
Xnand nand2 e a n1
Xnor nor2 en a n2
N2 z n2 gnd w=8
N1 vdd n1 z w=32
.ends

.subckt tristate_4 e a z
Xeinv inverter e en
Xnand nand2 e a n1
Xnor nor2 en a n2
N2 z n2 gnd w=16
N1 vdd n1 z w=64
.ends

.subckt tristate_8 e a z
Xeinv inverter e en
Xnand nand2 e a n1
Xnor nor2 en a n2
N2 z n2 gnd w=32
N1 vdd n1 z w=128
.ends

/********************************************************************************
*********************************************************************************
***
***  AND gates
***
*********************************************************************************
********************************************************************************/

.subckt and2 a b z
Xnand nand2 a b n1
Xinv inverter n1 z
.ends

.subckt and3 a b c z
Xnand nand3 a b c n1
Xinv inverter n1 z
.ends

.subckt and4 a b c d z
Xnand nand4 a b c d n1
Xinv inverter n1 z
.ends

/********************************************************************************
*********************************************************************************
***
***  NAND gates
***
*********************************************************************************
********************************************************************************/

.subckt nand2 a b z
Na z a n1
Nb n1 b gnd
Pa vdd a z
Pb vdd b z
.ends

.subckt nand3 a b c z
Na z a n1
Nb n1 b n2
Nc n2 c gnd
Pa vdd a z
Pb vdd b z
Pc vdd c z
.ends

.subckt nand4 a b c d z
Na z a n1
Nb n1 b n2
Nc n2 c n3
Nd n3 d gnd
Pa vdd a z
Pb vdd b z
Pc vdd c z
Pd vdd d z
.ends

/********************************************************************************
*********************************************************************************
***
***  OR gates
***
*********************************************************************************
********************************************************************************/

.subckt or2 a b z
Xnor nor2 a b n1
Xinv inverter n1 z
.ends

.subckt or3 a b c z
Xnor nor3 a b c n1
Xinv inverter n1 z
.ends

.subckt or4 a b c d z
Xnor nor4 a b c d n1
Xinv inverter n1 z
.ends

/********************************************************************************
*********************************************************************************
***
***  NOR gates
***
*********************************************************************************
********************************************************************************/

.subckt nor2 a b z
Na z a gnd
Nb z b gnd
Pa vdd a n1
Pb n1 b z
.ends

.subckt nor3 a b c z
Na z a gnd
Nb z b gnd
Nc z c gnd
Pa vdd a n1
Pb n1 b n2
Pc n2 c z
.ends

.subckt nor4 a b c d z
Na z a gnd
Nb z b gnd
Nc z c gnd
Na z d gnd
Pa vdd a n1
Pb n1 b n2
Pc n2 c n3
Pd n3 d z
.ends

/********************************************************************************
*********************************************************************************
***
***  XOR gates
***
*********************************************************************************
********************************************************************************/

.subckt xor2 a b z
Xnor nor2 a b nNor
Na z a n1
Nb n1 b gnd
Nnor z nNor gnd
Pa vdd a n2
Pb vdd b n2
Pnor n2 nNor z
.ends

.subckt xnor2 a b z
Xnand nand2 a b nNand
Nnand z nNand n1
Nb n1 b gnd
Na n1 a gnd
Pnand vdd nNand z
Pa vdd a n2
Pb n2 b z
.ends

/********************************************************************************
*********************************************************************************
***
***  AOI/OAI gates
***
*********************************************************************************
********************************************************************************/

.subckt aoi21 a1 a2 b z
Na z a n1
Nb n1 b gnd
Nc z c gnd
Pa vdd a n2
Pb vdd b n2
Pc n2 c z
.ends

.subckt oai21 a1 a2 b z
Na z a n1
Nb z b n1
Nc n1 c gnd
Pa vdd a n2
Pb n2 b z
Pc vdd c z
.ends

/********************************************************************************
*********************************************************************************
***
***  Muxes
***
*********************************************************************************
********************************************************************************/

.subckt mux2 s d0 d1 z
Xsinv inverter s sn
Xnand0 nand2 d0 sn n1
Xnand1 nand2 d1 s n2
Xnandfinal nand2 n1 n2 z
.ends

.subckt mux4 s0 s1 d0 d1 d2 d3 z
Xsinv inverter s0 s1 sn0 sn1
Xnand0 nand3 d0 sn0 sn1 n1
Xnand1 nand3 d1 s0 sn1 n2
Xnand2 nand3 d2 sn0 s1 n3
Xnand3 nand3 d3 s0 s1 n3
Xnandfinal nand4 n1 n2 n3 n4 z
.ends

/********************************************************************************
*********************************************************************************
***
***  Registers
***
*********************************************************************************
********************************************************************************/

.subckt dreg d clk q
Xmaster dlatchn d clk n1
Xslave dlatch n1 clk q
.ends

.subckt dlatch d clk q
Xmux mux2 clk q d q
.ends

.subckt dlatchn d clk q
Xmux mux2 clk d q q
.ends
